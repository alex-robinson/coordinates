      FUNCTION XDIST(RLAT, RLON)
C
C  PURPOSE :     TO RETURN X DISTANCE IN METERS FROM THE GRID ORIGIN
C                DESCRIBED BY THE COMMON BLOCK / GPARM /, GIVEN
C                LATITUDE AND LONGITUDE
C  ARGUMENTS:
C                RLAT - LATITUDE OF POINT
C                RLON - LONGITUDE (WEST) OF POINT
C                RPARM, IPARM - ARRAYS CONTAINING BATHYMETRIC GRID
C                       PARAMETERS AS DESCRIBED IN SUBROUTINE RGRID
C
C  COMMON BLOCK:  /GPARM/ RPARM(23),IPARM(54)
C
      COMMON /GPARM/ RPARM(23), IPARM(54)
      DATA PI /3.1415927/
      DATA DTR /0.017453293/
      ALPHA = RPARM(7) * DTR
C
C  FIND XPRIME, YPRIME - DISTANCES FROM THE ORIGIN OF THE STANDARD
C  BATHYMETRIC GRID
C
C
C  TRANSFORMATION FOR APPROXIMATE POLYCONIC PROJECTION
C
      IF(IPARM(45).EQ.0) THEN
      DLAT = RLAT - RPARM(1)
      DLON = RPARM(2) - RLON
       XP = RPARM(8) * DLON + RPARM(9) * DLAT + 
     1   RPARM(10) * DLON * DLAT + RPARM(11) * DLON ** 2
       YP = RPARM(12) * DLON + RPARM(13) * DLAT + RPARM(14) * DLAT *
     1     DLON + RPARM(15) * DLON ** 2
       XP=XP*1000.
       YP=YP*1000.
      ENDIF
C
C  TRANSFORMATION FOR LAMBERT CONFORMAL CONIC PROJECTION
C
      IF(IPARM(45).EQ.1) THEN
       R=(TAN(PI/4.-DTR*RLAT/2.))**RPARM(11)
       XP=RPARM(12)*R*SIN(RPARM(11)*(RPARM(8)-DTR*RLON))
       YP=-RPARM(12)*R*COS(RPARM(11)*(RPARM(8)-DTR*RLON))
       XP=XP-RPARM(13)
       YP=YP-RPARM(14)
      ENDIF

C
C  TRANSFORM TO 'UNPRIMED' SYSTEM
C
C  FIRST ROTATE
C
      XDIST = XP*COS(ALPHA) + YP*SIN(ALPHA)
C
C  NOW TRANSLATE
C
      XDIST = XDIST + IPARM(3) * RPARM(3)
      RETURN
      END
